##  -*- python -*-
##  Windstille - A Sci-Fi Action-Adventure Game
##  Copyright (C) 2000,2005 Ingo Ruhnke <grumbel@gmx.de>
##
##  This program is free software: you can redistribute it and/or modify
##  it under the terms of the GNU General Public License as published by
##  the Free Software Foundation, either version 3 of the License, or
##  (at your option) any later version.
##  
##  This program is distributed in the hope that it will be useful,
##  but WITHOUT ANY WARRANTY; without even the implied warranty of
##  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
##  GNU General Public License for more details.
##  
##  You should have received a copy of the GNU General Public License
##  along with this program.  If not, see <http://www.gnu.org/licenses/>.

import glob

Import('conf_env', 'features')

SConscript('scripting/SConscript')

def expand_path(*args):
    lst = []
    for i in args:
        lst += glob.glob(i)
    return lst

# libutil
util_env = conf_env.Clone()
util_env.Append(CPPPATH=['.'])
util_lib = util_env.StaticLibrary('util', expand_path('lisp/*.cpp',  'util/*.cpp'))

# libphysfs
physfs_env = conf_env.Clone()
physfs_env.Append(CPPPATH=['.'])
physfs_env.ParseConfig('sdl-config --cflags --libs')
physfs_lib = physfs_env.StaticLibrary('physfs', expand_path('physfs/*.cpp'))

# libmath
math_env = conf_env.Clone()
math_env.Append(CPPPATH=['.'])
math_lib = math_env.StaticLibrary('math', expand_path('math/*.cpp'))

# libnavgraph
navgraph_env = conf_env.Clone()
navgraph_env.Append(CPPPATH=['.'])
navgraph_lib = navgraph_env.StaticLibrary('navgraph', expand_path('navigation/*.cpp'))

# libparticles
particles_env = conf_env.Clone()
particles_env.Append(CPPPATH=['.'])
particles_lib = particles_env.StaticLibrary('particles', expand_path('particles/*.cpp'))

# libdisplay
display_env = conf_env.Clone()
display_env.Append(CPPPATH=['.'])
display_env.ParseConfig('sdl-config --cflags --libs')
display_env.ParseConfig('freetype-config --libs --cflags')
display_lib = display_env.StaticLibrary('display', 
                                        expand_path('font/*.cpp', 'display/*.cpp', 'sprite2d/*.cpp', 'sprite3d/*.cpp'))


env = conf_env.Clone()
env.ParseConfig('sdl-config --cflags --libs')
env.ParseConfig('freetype-config --libs --cflags')
env.Append(LIBS=[particles_lib, navgraph_lib, display_lib, physfs_lib, util_lib, math_lib,
                 'GL', 'GLU', 'GLEW',
                 'squirrel', 'physfs', 'SDL_image', 'openal', 'ogg', 'vorbis', 'vorbisfile', 'png', 'boost_signals-mt'])
env.Append(LIBPATH=['../external/'])
env.Append(CPPPATH=['.', '..', '../external/SQUIRREL2/include/'])

if features['64bit']:
    env.Append(CPPDEFINES = '_SQ64')

if features['cwiid']:
    env.Append(CPPDEFINES = 'HAVE_CWIID')
    env.Append(LIBS = 'cwiid')

env.Program('../windstille', expand_path('app/*.cpp',
                                         'armature/*.cpp',
                                         'collision/*.cpp',
                                         'engine/*.cpp',
                                         'gui/*.cpp',
                                         'hud/*.cpp',
                                         'input/*.cpp',
                                         'objects/*.cpp',
                                         'properties/*.cpp',
                                         'screen/*.cpp',
                                         'scripting/*.cpp',
                                         'sound/*.cpp',
                                         'tile/*.cpp'))

debug_cxxflags = ["-O0", "-g", "-Wall", "-ansi", "-pedantic", "-Wnon-virtual-dtor", "-Werror"] # "-Wconversion",
# -W: unused parameter
# ['-Wall', '-Werror', '-O2', '-g'],
editor_env = Environment(CPPPATH=['.'],
                         CXXFLAGS=debug_cxxflags,
                         LIBS = [particles_lib, navgraph_lib, display_lib, util_lib, physfs_lib, math_lib,
                                 'GL', 'GLEW', 'SDL_image', 'physfs'])

editor_env.ParseConfig('pkg-config libpng --libs --cflags')
editor_env.ParseConfig('sdl-config --cflags --libs')
editor_env.ParseConfig('Magick++-config --libs --cppflags')
editor_env.ParseConfig('pkg-config --cflags --libs libcurl')
editor_env.ParseConfig('pkg-config --cflags --libs gtkmm-2.4 gtkglextmm-1.2')

editor_env.Program('../windstille-editor', expand_path('editor/*.cpp'))

# EOF #
